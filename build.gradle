apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
version = '2.0.0'
group = "com.github.markymarkmcdonald"
archivesBaseName = "FactoryJill"

repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-beanutils:commons-beanutils:1.9.2'

    testCompile 'com.google.guava:guava:11.0-rc1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Factory Jill'
                packaging 'jar'
                artifactId 'FactoryJill'
                description 'A library for setting up Java objects as test data'
                url 'https://github.com/MarkyMarkMcDonald/FactoryJill'

                scm {
                    connection 'scm:git:git@github.com:markymarkmcdonald/factoryjill.git'
                    developerConnection 'scm:git:git@github.com:markymarkmcdonald/factoryjill.git'
                    url 'git@github.com:markymarkmcdonald/factoryjill.git'
                }

                licenses {
                    license {
                        name 'MIT license'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'MarkyMarkMcDonald'
                        name 'Mark McDonald'
                        email 'mark.mcdonald.ga@gmail.com'
                        organizationUrl 'https://github.com/MarkyMarkMcDonald'
                    }
                }
            }
        }
    }
}